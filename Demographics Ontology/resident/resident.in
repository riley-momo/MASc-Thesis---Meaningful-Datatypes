% Resident Axiom 1: The resides in relation is defined only for crowds, geopolitical entities, and timepoints.
(all c all gp all t (resides_in(c, gp, t) ->
	(crowd(c) & geopolitical_entity(gp) & timepoint(t)))).

% Resident Axiom 2: Crowds may only reside in geopolitical entities during a timepoint inside that entities occupation of a geospatial region.
(all c all gp all t (resides_in(c, gp, t) -> 
	(exists o exists gs (occupation_of(o, gs, gp) & between(beginOf(o), t, endOf(o)))))).

% Resident Axiom 3: A crowd cannot reside in multiple disjoint geopolitical entities at a timepoint.
(all c all gp1 all gp2 all t 
	((resides_in(c, gp1, t) &
	resides_in(c, gp2, t)) ->
		-geopol_disjoint(gp1, gp2, t))).

% Resident Axiom 4: A crowd must reside in some geopolitical entity at a timepoint.
(all c (crowd(c) -> (exists gp exists t resides_in(c, gp, t)))).

% Resident Axiom 5: Subcrowd and sub-geopolitical entity relationship
(all c1 all c2 all gp1 all gp2 all t
	((subcrowd(c1, c2) &
	resides_in(c1, gp1, t) &
	resides_in(c2, gp2, t)) ->
		subgeopol(gp1, gp2, t))).